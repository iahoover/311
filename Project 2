unsigned long previousMillisLED1 = 0;
unsigned long previousMillisLED2 = 0;
int intervalLED1 = 1000; //Default interval
int intervalLED2 = 1000; //Default interval
bool led1State = false; // these booleans track the states of the LEDs
bool led2State = false;
bool waitingForInput = true; // to manage the prompts
int currentLED = 0;  // to manage which LED the user is configuring

// cyclic executive prototypes
void taskSerial();   // input logic
void taskLED1();     // LED1 timing logic
void taskLED2();     // LED2 timing logic

// function pointer array 
typedef void (*TaskFn)();
TaskFn tasks[] = { taskSerial, taskLED1, taskLED2 };
const uint8_t NUM_TASKS = sizeof(tasks) / sizeof(tasks[0]);

void setup() {
  Serial.begin(9600);
  pinMode(8, OUTPUT);  
  pinMode(9, OUTPUT);
  Serial.println("What LED? (1-2)");
}

void loop() {
  // cyclic executive
  static uint8_t rr = 0;
  tasks[rr]();                    // run one task per tick
  rr = (rr + 1) % NUM_TASKS;     // next task
}

void taskSerial() {
  if (waitingForInput) {
    if (Serial.available() > 0) {
      String strInput = Serial.readStringUntil('\n');
      int userInput = strInput.toInt();
      if (userInput == 1 || userInput == 2) {
        currentLED = userInput;
        Serial.println("What Interval? (msec)");
        waitingForInput = false;
      } else {
        Serial.println("Invalid Input. Please choose between LED 1 or 2.");
      }
    }
  } else {
    if (Serial.available() > 0) {
      String strDelayInput = Serial.readStringUntil('\n');
      int delayInput = strDelayInput.toInt();
      if (delayInput > 0) {
        if (currentLED == 1) {
          intervalLED1 = delayInput;
          Serial.println("Interval set for LED 1.");
        } else if (currentLED == 2) {
          intervalLED2 = delayInput;
          Serial.println("Interval set for LED 2.");
        }
        Serial.println("What LED? (1-2)"); 
        waitingForInput = true; 
      } else {
        Serial.println("Invalid Interval. Please ensure your interval is in ms.");
      }
    }
  }
}

void taskLED1() {
  unsigned long currentMillis = millis();
  if (currentMillis - previousMillisLED1 >= (unsigned long)intervalLED1) {
    previousMillisLED1 = currentMillis;
    led1State = !led1State;
    digitalWrite(8, led1State ? HIGH : LOW);
  }
}

void taskLED2() {
  unsigned long currentMillis = millis();
  if (currentMillis - previousMillisLED2 >= (unsigned long)intervalLED2) {
    previousMillisLED2 = currentMillis;
    led2State = !led2State;
    digitalWrite(9, led2State ? HIGH : LOW);
  }
}
